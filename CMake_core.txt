set(CMAKE_DEBUG_POSTFIX "d" CACHE STRING "add a postfix, usually d on windows.")
set(CMAKE_RELEASE_POSTFIX "" CACHE STRING "add a postfix, usually empty on windows.")

set(G3_SHARED_LIB OFF CACHE BOOL "Build shared library." FORCE)
set(BUILD_SHARED_LIBS OFF CACHE BOOL "Force compiler to create static libs." FORCE)
set(ADD_FATAL_EXAMPLE OFF CACHE BOOL "Fatal (fatal-crashes/contract) examples " FORCE)
set(ADD_BUILD_WIN_SHARED OFF CACHE BOOL "Build shared library on Windows" FORCE)
set(USE_DYNAMIC_LOGGING_LEVELS ON CACHE BOOL "Turn ON/OFF log levels. An disabled level will not push logs of that level to the sink. By default dynamic logging is disabled" FORCE)
set(CHANGE_G3LOG_DEBUG_TO_DBUG ON CACHE BOOL "Use DBUG logging level instead of DEBUG. By default DEBUG is the debugging level" FORCE)
add_subdirectory(extern/fwcore/extern/g3log)

set(ASSIMP_BUILD_ASSIMP_TOOLS OFF CACHE BOOL "" FORCE)
set(ASSIMP_BUILD_SAMPLES OFF CACHE BOOL "" FORCE)
set(ASSIMP_BUILD_TESTS OFF CACHE BOOL "" FORCE)
add_subdirectory(extern/fwcore/extern/assimp)

set(VISCOM_CLIENTGUI ON CACHE BOOL "Use ImGui on clients.")
set(VISCOM_SYNCINPUT ON CACHE BOOL "Synchronize input from master to clients.")
set(VISCOM_CLIENTMOUSECURSOR ON CACHE BOOL "Show the mouse cursor on clients.")
set(VISCOM_APP_NAME "${APP_NAME}" CACHE STRING "Name of the application to be build.")
set(VISCOM_LOCAL_ONLY ON CACHE BOOL "Only do a local build without calibration information.")
set(VISCOM_USE_SGCT ON CACHE BOOL "Use SGCT for local builds.")
set(VISCOM_CONFIG_NAME "single" CACHE STRING "Name/directory of the configuration files to be used.")
set(VISCOM_VIRTUAL_SCREEN_X 1920 CACHE INTEGER "Virtual screen size in x direction.")
set(VISCOM_VIRTUAL_SCREEN_Y 1080 CACHE INTEGER "Virtual screen size in y direction.")
set(VISCOM_NEAR_PLANE_SIZE_X "1.7778" CACHE STRING "Size of the near plane in x direction.")
set(VISCOM_NEAR_PLANE_SIZE_Y "1.0" CACHE STRING "Size of the near plane in y direction.")
# This could be changed to "off" if the default target doesn't need the tuio library
set(TUIO_LIB ON CACHE BOOL "Use TUIO input library")
set(TUIO_PORT 3333 CACHE STRING "UDP Port for TUIO to listen on")

# Build-flags.
if(UNIX)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++1z")
  set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -O0 -Wall -Wno-unused-function -Wno-unused-parameter -Wextra -Wpedantic -std=c++1z")
  set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -std=c++1z")

  # add compiler-options for AppleClang to ignore linker warnings from assimp on macOS
  if (CMAKE_CXX_COMPILER_ID MATCHES "AppleClang")
    add_compile_options(-fvisibility=hidden -fvisibility-inlines-hidden)
  endif()
elseif(MSVC)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W3 /EHsc /MP /std:c++latest")
endif()

file(GLOB EXTERN_SOURCES_CORE
    extern/fwcore/extern/imgui/imgui.cpp
    extern/fwcore/extern/imgui/imgui_draw.cpp
    extern/fwcore/extern/imgui/imgui_demo.cpp)

if (${VISCOM_USE_SGCT})
    list(APPEND COMPILE_TIME_DEFS VISCOM_USE_SGCT)
    set(VISCOM_SGCT_WRAPPER_DIR ${PROJECT_SOURCE_DIR}/extern/fwcore/src_sgct/)
else()
    option(GLFW_BUILD_DOCS OFF)
    option(GLFW_BUILD_EXAMPLES OFF)
    option(GLFW_BUILD_TESTS OFF)
    add_subdirectory(extern/fwcore/extern/glfw)

    list(APPEND COMPILE_TIME_DEFS GLEW_STATIC)
    list(APPEND EXTERN_SOURCES_CORE extern/fwcore/src_nosgct/glew/src/glew.c)
    set(VISCOM_SGCT_WRAPPER_DIR ${PROJECT_SOURCE_DIR}/extern/fwcore/src_nosgct/)
endif()


file(GLOB_RECURSE SHADER_FILES_CORE ${PROJECT_SOURCE_DIR}/extern/fwcore/resources/shader/*.*)
file(GLOB_RECURSE SRC_FILES_CORE
    ${PROJECT_SOURCE_DIR}/extern/fwcore/src/core/*.h
    ${PROJECT_SOURCE_DIR}/extern/fwcore/src/core/*.cpp
    ${PROJECT_SOURCE_DIR}/extern/fwcore/src/core/*.inl
    ${VISCOM_SGCT_WRAPPER_DIR}/core/*.h
    ${VISCOM_SGCT_WRAPPER_DIR}/core/*.cpp
    ${VISCOM_SGCT_WRAPPER_DIR}/core/*.inl)
source_group("extern\\core" FILES ${EXTERN_SOURCES_CORE})
source_group("shader\\core" FILES ${SHADER_FILES_CORE})

if (${VISCOM_LOCAL_ONLY})
    list(REMOVE_ITEM SRC_FILES_CORE "${PROJECT_SOURCE_DIR}/extern/fwcore/src/core/CalibrationVertices.h")
    list(REMOVE_ITEM SRC_FILES_CORE "${PROJECT_SOURCE_DIR}/extern/fwcore/src/core/SlaveNodeInternal.h")
    list(REMOVE_ITEM SRC_FILES_CORE "${PROJECT_SOURCE_DIR}/extern/fwcore/src/core/SlaveNodeInternal.cpp")
    list(REMOVE_ITEM SRC_FILES_CORE "${PROJECT_SOURCE_DIR}/extern/fwcore/src/core/OpenCVParserHelper.h")
    list(REMOVE_ITEM SRC_FILES_CORE "${PROJECT_SOURCE_DIR}/extern/fwcore/src/core/OpenCVParserHelper.cpp")
endif()

foreach(f ${SRC_FILES_CORE})
    file(RELATIVE_PATH SRCGR ${PROJECT_SOURCE_DIR}/extern/fwcore ${f})
    string(REGEX REPLACE "(.*)(/[^/]*)$" "\\1" SRCGR ${SRCGR})
    string(REPLACE / \\ SRCGR ${SRCGR})
    string(REPLACE src_sgct src SRCGR ${SRCGR})
    string(REPLACE src_nosgct src SRCGR ${SRCGR})
    source_group("${SRCGR}" FILES ${f})
endforeach()


if (${VISCOM_USE_SGCT})
    find_package(OpenGL REQUIRED)
    find_library(SGCT_RELEASE_LIBRARY NAMES sgct libsgct PATHS $ENV{SGCT_ROOT_DIR}/lib REQUIRED)
    find_library(SGCT_DEBUG_LIBRARY NAMES sgctd libsgctd PATHS $ENV{SGCT_ROOT_DIR}/lib REQUIRED)
    find_path(SGCT_INCLUDE_DIRECTORY NAMES sgct PATHS $ENV{SGCT_ROOT_DIR}/include NO_DEFAULT_PATH REQUIRED)

    set(SGCT_LIBS
        debug ${SGCT_DEBUG_LIBRARY}
        optimized ${SGCT_RELEASE_LIBRARY})

    list(APPEND CORE_LIBS ${SGCT_LIBS} ${OPENGL_LIBRARY} ws2_32)
    list(APPEND CORE_INCLUDE_DIRS ${SGCT_INCLUDE_DIRECTORY})
else()
    list(APPEND CORE_LIBS glfw ${GLFW_LIBRARIES} opengl32)
    list(APPEND CORE_INCLUDE_DIRS extern/fwcore/extern/glm extern/fwcore/extern/glfw/include)
endif()

list(APPEND CORE_INCLUDE_DIRS
    src
    extern/fwcore/src
    ${VISCOM_SGCT_WRAPPER_DIR}
    extern/fwcore/extern/g3log/src
    ${CMAKE_CURRENT_BINARY_DIR}/extern/fwcore/extern/g3log/include
    extern/fwcore/extern/imgui
    extern/fwcore/extern/stb
    extern/fwcore/extern/assimp/include
    ${CMAKE_CURRENT_BINARY_DIR}/extern/fwcore/extern/assimp/include)

list(APPEND CORE_LIBS g3logger assimp)

if(MSVC)
    list(APPEND COMPILE_TIME_DEFS _CRT_SECURE_NO_WARNINGS _SCL_SECURE_NO_WARNINGS)
    set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${APP_NAME})
endif()

if (${VISCOM_CLIENTMOUSECURSOR})
    list(APPEND COMPILE_TIME_DEFS VISCOM_CLIENTGUI VISCOM_SYNCINPUT VISCOM_CLIENTMOUSECURSOR)
else()
    if(${VISCOM_SYNCINPUT})
        list(APPEND COMPILE_TIME_DEFS VISCOM_SYNCINPUT)
    endif()
    if(${VISCOM_CLIENTGUI})
        list(APPEND COMPILE_TIME_DEFS VISCOM_CLIENTGUI)
    endif()
endif()

if (${VISCOM_LOCAL_ONLY})
    list(APPEND COMPILE_TIME_DEFS VISCOM_LOCAL_ONLY)
endif()

if(${TUIO_LIB})
    add_subdirectory(extern/fwcore/extern/tuio)
    list(APPEND COMPILE_TIME_DEFS WITH_TUIO)
    list(APPEND CORE_LIBS libTUIO)
    list(APPEND CORE_INCLUDE_DIRS extern/fwcore/extern/tuio/TUIO extern/fwcore/extern/tuio/oscpack)
endif()

macro(copy_core_lib_dlls APP_NAME)
    if (${TUIO_LIB})
        add_custom_command(TARGET ${APP_NAME} POST_BUILD COMMAND ${CMAKE_COMMAND} -E copy_if_different 
            ${PROJECT_BINARY_DIR}/extern/fwcore/extern/tuio/$<CONFIGURATION>/libTUIO$<$<CONFIG:Debug>:d>.dll ${PROJECT_BINARY_DIR})
    endif()
endmacro()

